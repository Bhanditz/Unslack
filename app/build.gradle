buildscript {
  dependencies {
    classpath "io.objectbox:objectbox-gradle-plugin:$objectboxVersion"
  }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android.sourceSets.main.java.srcDirs += 'build/generated/source/objectbox'

kapt {
  correctErrorTypes = true
}

project.android.buildTypes.all { buildType ->
  buildType.javaCompileOptions.annotationProcessorOptions.arguments =
      [// Validation is disabled in production to remove the runtime overhead
       validateEpoxyModelUsage     : String.valueOf(buildType.name == 'debug'),
       requireHashCodeInEpoxyModels: "true",
       requireAbstractEpoxyModels  : "true",]
}

android {
  compileSdkVersion 27
  buildToolsVersion '27.0.0'
  defaultConfig {
    applicationId "com.vincentcarrier.todo"
    minSdkVersion 21
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
  if (project.hasProperty('devBuild')) {
    splits.density.enable = false
    aaptOptions.cruncherEnabled = false
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.1.51'
  implementation 'org.jetbrains.anko:anko:0.10.1'

  implementation "com.android.support:appcompat-v7:27.0.0"
  implementation "com.android.support:recyclerview-v7:27.0.0"
  implementation "com.android.support:customtabs:27.0.0"
  implementation "com.android.support:design:27.0.0"
  implementation 'com.android.support.constraint:constraint-layout:1.0.2'

  implementation "android.arch.lifecycle:runtime:$android_lifecycle_version"
  implementation "android.arch.lifecycle:extensions:$android_lifecycle_version"
  kapt "android.arch.lifecycle:compiler:$android_lifecycle_version"

  implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
  implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
  implementation 'com.github.pwittchen:reactivenetwork-rx2:0.12.1'

  implementation 'com.squareup.retrofit2:retrofit:2.3.0'
  implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
  implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
  implementation 'com.google.code.gson:gson:2.8.2'
  implementation 'com.squareup.okhttp3:okhttp:3.9.0'
  implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'

  // NoSQL database
  implementation "io.objectbox:objectbox-kotlin:$objectboxVersion"
  kapt "io.objectbox:objectbox-processor:$objectboxVersion"
  implementation 'io.objectbox:objectbox-rxjava:0.9.8'
  debugImplementation "io.objectbox:objectbox-android-objectbrowser:$objectboxVersion"
  releaseImplementation "io.objectbox:objectbox-android:$objectboxVersion"

  // OAuth library using Chrome Custom Tabs
  implementation 'net.openid:appauth:0.2.0'

  // Used to move RecyclerView logic into the ViewModel
  implementation 'com.airbnb.android:epoxy:2.7.2'
  kapt 'com.airbnb.android:epoxy-processor:2.7.2'

  implementation 'joda-time:joda-time:2.9.9'


  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:2.8.47'
  testImplementation "com.nhaarman:mockito-kotlin-kt1.1:1.5.0"
  testImplementation "org.robolectric:robolectric:3.5.1"
  testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.0'

  androidTestImplementation 'com.android.support.test:runner:1.0.1'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
  androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
      exclude group: 'com.android.support', module: 'support-annotations'
      exclude group: 'com.android.support', module: 'support-v4'
      exclude group: 'com.android.support', module: 'design'
      exclude group: 'com.android.support', module: 'recyclerview-v7'
  }
  androidTestImplementation('com.android.support.test.espresso:espresso-intents:2.2') {
      exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
}

apply plugin: 'io.objectbox'